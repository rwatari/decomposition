<!DOCTYPE html>
<html>
  <head>
    <meta charset="utf-8">
    <title>Decomposition</title>
  </head>
  <body>
    <canvas id="canvas"></canvas>
    <script id="v-shader" type="x-shader/x-vertex">
      // position fed in as pixel coords
      attribute vec2 a_position;
      attribute vec2 a_texCoord;

      uniform vec2 u_resolution;

      //passes v_texCoord from vshader to fshader
      varying vec2 v_texCoord;

      void main() {
        // Converting from pixel coords to clipspace
        // vec2 zeroToOne = a_position / u_resolution;
        // vec2 zeroToTwo = zeroToOne * 2.0;
        // vec2 clipSpace = zeroToTwo - 1.0;
        // Convert clipSpace vec to vec4 and set (0, 0) to upper left
        // gl_Position = vec4(clipSpace * vec2(1, -1), 0, 1);
        gl_Position = vec4(a_position, 0, 1);
        v_texCoord = a_texCoord;
      }
    </script>

    <script id="f-shader-gray" type="x-shader/x-fragment">
      precision mediump float;

      // image texture
      uniform sampler2D u_image;

      // texture coords
      varying vec2 v_texCoord;

      void main() {
        vec4 rgbColors = texture2D(u_image, v_texCoord);
        float grayScale = (rgbColors.r + rgbColors.g + rgbColors.b) / 3.0;
        gl_FragColor = vec4(vec3(grayScale), 1.);
      }
    </script>

    <script id="f-shader-normal" type="x-shader/x-fragment">
      precision mediump float;

      // image texture
      uniform sampler2D u_image;

      // texture coords
      varying vec2 v_texCoord;

      void main() {
        gl_FragColor = vec4(texture2D(u_image, v_texCoord).rgb, 1.0);
      }
    </script>

    <script id="f-shader-laplacian2" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D u_image;
      uniform vec2 u_textureSize;
      varying vec2 v_texCoord;

      void main() {
        // does this need to be converted to and from concentration [-1, 1]?
        vec2 onePixel = vec2(1.0, 1.0) / u_textureSize;
        vec4 pixelAt = texture2D(u_image, v_texCoord);
        float lapl =
          texture2D(u_image, v_texCoord + onePixel * vec2( 1.,  0.)).r +
          texture2D(u_image, v_texCoord + onePixel * vec2(-1.,  0.)).r +
          texture2D(u_image, v_texCoord + onePixel * vec2( 0.,  1.)).r +
          texture2D(u_image, v_texCoord + onePixel * vec2( 0., -1.)).r -
          pixelAt.r * 4.0 + 0.5;
        gl_FragColor = vec4(vec3(lapl), 1.);
      }
    </script>

    <script id="f-shader-laplacian" type="x-shader/x-fragment">
      precision mediump float;

      uniform sampler2D u_image;
      uniform vec2 u_textureSize;
      varying vec2 v_texCoord;

      float extractConc(vec2 pixelSize, float xOffset, float yOffset) {
        float pixelData = texture2D(u_image, v_texCoord + pixelSize * vec2(xOffset,yOffset)).r;
        return  pixelData * 2. - 1.;
      }

      float encodeConc(float conc) {
        return (conc + 1.) * 0.5;
      }

      void main() {
        vec2 onePixel = vec2(1.0, 1.0) / u_textureSize;
        float c  = extractConc(onePixel,  0.,  0.);
        float n  = extractConc(onePixel,  0.,  1.);
        float e  = extractConc(onePixel,  1.,  0.);
        float w  = extractConc(onePixel, -1.,  0.);
        float s  = extractConc(onePixel,  0., -1.);
        float diags =
          extractConc(onePixel,  1.,  1.) +
          extractConc(onePixel, -1.,  1.) +
          extractConc(onePixel,  1., -1.) +
          extractConc(onePixel, -1., -1.);
        float nneewwss =
          extractConc(onePixel,  0.,  2.) +
          extractConc(onePixel,  2.,  0.) +
          extractConc(onePixel, -2.,  0.) +
          extractConc(onePixel,  0., -2.);

        float news = n + e + w + s;
        // gamma = 0.5, D = 2.
        float delta =
          2. * (
            pow(n, 3.) + pow(e, 3.) + pow(w, 3.) + pow(s, 3.)
            - news - 4. * (pow(c, 3.) - c)
            + 0.5 * ( news + 12. * c - nneewwss - 2. * diags)
          );
        // time delta is 50. ms
        float newConc = c + delta * 50. * 0.001;
        gl_FragColor = vec4(vec3(encodeConc(newConc)), 1.0);
      }
    </script>

    <script src="lib/bundle.js" charset="utf-8"></script>
  </body>
</html>
